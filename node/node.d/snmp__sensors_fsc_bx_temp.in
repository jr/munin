#!@@PERL@@ -w
#
# Plugin to fetch temperature data from the SNMP agent on the management
# blade on Fujitsu Simens BX series blade servers
#
# $Log$
# Revision 1.3  2004/11/12 20:28:03  ilmari
# No debugging info by default
#
# Revision 1.2  2004/11/12 20:23:57  ilmari
# Rename Munin::Node::SNMP to Munin::Plugin::SNMP
#
# Revision 1.1  2004/10/27 18:58:26  ilmari
# Added SNMP plugins for Fujitsu Siemens ServerView temperature and fan sensors
#
#
#%# family=snmpauto
#%# capabilities=snmpconf

use strict;
use Munin::Plugin::SNMP;
use Net::SNMP qw(oid_lex_sort);

# The OIDs we're after
my $tempBase = '1.3.6.1.4.1.7244.1.1.1.4.6.1.1';

# Subtables
my $tempCabinetId          = 1;
my $tempSensorNumber       = 2;
my $tempSensorStatus       = 3;
my $tempSensorDesignation  = 4;
my $tempUpperWarningLevel  = 5;
my $tempUpperCriticalLevel = 6;
my $tempCurrentValue       = 7;

# Magic values
my $tempSensorUnknown = 1;
my $tempSensorDisabled = 2;
my $tempSensorUnavailable = 99;

if (defined $ARGV[0] and $ARGV[0] eq 'snmpconf') {
    print "index   $tempBase.\n";
    # Require known, enabled and available sensors
    print "require $tempBase.$tempSensorStatus. ^[3-9]|[1-8][0-9]|9[0-8]\$\n";

    exit 0;
}

my $DEBUG = 0;

my ($session, $error) = Munin::Plugin::SNMP->session();

if ($error) {
    die "# Error: $error\n";
}

my $temps =  $session->get_hash(-baseoid => $tempBase,
				-cols    => { $tempCabinetId          => 'cabinet',
					      $tempSensorNumber       => 'number',
					      $tempSensorStatus       => 'status',
					      $tempUpperWarningLevel  => 'warning',
					      $tempUpperCriticalLevel => 'critical',
					      $tempCurrentValue       => 'value',
					      $tempSensorDesignation  => 'label',
					    },
			       ) or die "# Error: ". $session->error();

for my $key (keys %$temps) {
    my $temp = $temps->{$key};
    $temp->{info} = "Blade $temp->{cabinet} sensor $temp->{number}";
    $temp->{label} = "Blade $temp->{cabinet} $temp->{label}";
    # Delete sensors with unknown, disabled or unavailable status
    delete $temps->{$key}
      if $temp->{status} == $tempSensorUnknown ||
	$temp->{status} == $tempSensorDisabled ||
	$temp->{status} == $tempSensorUnavailable;
}

if (defined $ARGV[0] and $ARGV[0] eq 'config') {
    print <<EOM;
graph_title Temperatures
graph_args -l 0
graph_vlabel degrees Celcius
graph_category sensors
EOM
    print 'graph_order ', join(' ', map { get_id($_) } oid_lex_sort keys %$temps), "\n";
    print 'host_name ', $session->hostname(), "\n";
      
    for my $sensor (keys %$temps) {
	my $id = get_id($sensor);
	for my $key (qw(label warning critical info)) {
	    print "$id.$key $temps->{$sensor}->{$key}\n";
	}
	print "$id.type GAUGE\n";
    }
} else {
    print get_id($_), '.value ', $temps->{$_}{value}, "\n"
      for keys %$temps;
}

sub get_id {
    (my $id = shift) =~ tr/\./_/;
    return 'temp'.$id;
}
