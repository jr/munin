#!/usr/bin/perl -w
#!@@PERL@@ -w
#
# Plugin to monitor the volume of mails delivered by postfix.
#
# Usage: copy or link into /etc/munin/node.d/
#
# Parameters:
#
# 	config   (required)
# 	autoconf (optional - used by munin-config)
#
# Config variables:
#
# 	logdir       - Which logfile to use
#	logfile      - What file to read in logdir
#
# Magic markers (optional - used by munin-config and some installation
# scripts):
#
#%# family=auto
#%# capabilities=autoconf

use strict;
use Munin::Plugin;

my $pos   = undef;
my $volume = 0;
my $LOGDIR  = $ENV{'logdir'}  || '/var/log';
my $LOGFILE = $ENV{'logfile'} || 'syslog';


if ( $ARGV[0] and $ARGV[0] eq "autoconf" ) {
    my $logfile;
    `which postconf >/dev/null 2>/dev/null`;
    if (!$?) {
	$logfile = "$LOGDIR/$LOGFILE";

	if (-f $logfile) {
            if (-r "$logfile") {
                print "yes\n";
                exit 0;
            } else {
                print "no (logfile '$logfile' not readable)\n";
            }
	} else {
	    print "no (logfile '$logfile' not found)\n";
	}
    } else {
	print "no (postfix not found)\n";
    }

    exit 1;
}


if ( $ARGV[0] and $ARGV[0] eq "config" ) {
    print "graph_title Postfix bytes throughput\n";
    print "graph_args --base 1000 -l 0\n";
    print "graph_vlabel bytes / \${graph_period}\n";
    print "graph_scale yes\n";
    print "graph_category postfix\n";
    print "volume.label throughput\n";
    print "volume.type DERIVE\n";
    print "volume.min 0\n";
    exit 0;
}


my $logfile = "$LOGDIR/$LOGFILE";

if (! -f $logfile) {
    print "delivered.value U\n";
    exit 1;
}

($pos,$volume) = restore_state();

$pos = $volume = 0 if !defined($volume);

$pos = parseLogfile ($logfile, $pos);

print "volume.value $volume\n";

save_state($pos,$volume);


sub parseLogfile {
    my ($fname, $start) = @_;

    my ($LOGFILE,$rotated) = tail_open($fname,$start);

    my $line;

    while ($line =<$LOGFILE>) {
	chomp ($line);

	if ($line =~ /qmgr.*from=.*size=([0-9]+)/) {
	    $volume += $1;
	}
    }
    return tail_close($LOGFILE);
}

# vim:syntax=perl
